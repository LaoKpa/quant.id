/* 
 * Copyright (C)2017 PT. Stosia Teknologi Investasi
 *
 * File ini  berlisensi  GNU GPL (General Public License)  versi 3. Artinya
 * kalau Anda  memodifikasi file ini, atau membuat karya lain yang berbasis
 * file ini  ("derivative work"),  maka Anda  wajib  mendistribusikan  kode
 * sumber ("source code")  modifikasi  atau karya  tersebut  kepada publik.
 * Hal ini untuk menjaga keterbukaan pengetahuan di masa mendatang.
 *
 * Silakan memakai kode ini untuk kepentingan apapun termasuk untuk mencari
 * profit,  asal tidak jahat.  Sebagai tambahan permintaan, kami menghimbau
 * untuk  TIDAK  menjual kode/file ini secara apa adanya  atau dalam bundel
 * produk yang dijual secara komersial, karena hal itu tidak etis.
 *
 * Untuk penjelasan lebih lanjut silakan bertanya kepada kami.  Untuk detil
 * lisensi GPLv3  silakan lihat file GPLv3-LICENSE.md  yang didistribusikan
 * bersama file ini.
 *
 * Author: Benny Prijono <benny@stosia.com>
 */
 
SetBarsRequired(sbrAll);

#include_once <..\..\quant.id\AFL\Tools.afl>
#include_once <..\..\quant.id\AFL\Common.afl>
#include_once <..\..\quant.id\AFL\ForeignFlow\ForeignFlow.afl>


stSetBackgroundTitle(Name());
stSetBackgroundSubtitle("Foreign Flow System");
stSetBackgroundSubsubtitle("© 2017 quant.id");

//
// Price candlesticks
//
_SECTION_BEGIN("Price");
	SetChartOptions(0,chartShowArrows|chartShowDates);
	_N(Title = StrFormat("© 2017 quant.id -- {{NAME}} - {{INTERVAL}} {{DATE}} Open %g, Hi %g, Lo %g, Close %g (%.1f%%) Vol " + WriteVal( V, 1.0 ) +" {{VALUES}}", 
						  O, H, L, C, SelectedValue( ROC( C, 1 )) ));
	Plot( C, "Close", colorDefault, styleNoTitle | GetPriceStyle() ); 
_SECTION_END();


//
// Total transaction value
//
_SECTION_BEGIN("Value (B)");
	intervalValue = stGetIntervalValue();
	
	maxIntervalValue = Max(HighestVisibleValue(intervalValue) * 2, 1);
	
	Plot( intervalValue, 
		  _DEFAULT_NAME(), 
		  ColorBlend( colorBlueGrey, GetChartBkColor(), 0.5  ),
		  styleHistogram | styleThick | styleOwnScale,
		  0,
		  maxIntervalValue,
		  0,
		  0,
		  5 );
	
_SECTION_END();


//
// Foreign transaction value, drawn over the transaction value above
//
_SECTION_BEGIN("Foreign Value (B)");
	ffIntervalValue = stGetFFIntervalValue();
	
	Plot( abs(ffIntervalValue), 
		  WriteIf(ffIntervalValue >= 0, "Net Buy (B)", "Net Sell (B)"), 
		  IIf( ffIntervalValue >= 0, colorBlue, colorOrange),
		  styleHistogram | styleThick | styleOwnScale | styleNoLabel,
		  Null,
		  maxIntervalValue,
		  0,
		  1,
		  3);
_SECTION_END();


/*
_SECTION_BEGIN("Daily Avg");
	AvgPeriodDays = Param("Avg Period Days", 2, 1, 600);
	
	FF_IntBuyValue = odbcGetArraySQL(" SELECT INT_BUY_VALUE, dtime " + 
								     " FROM v_adj_intraday_foreign " +
							         " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	FF_IntBuyShares = FF_IntBuyValue / Close;

	FF_DayBuyValue = SumDays(FF_IntBuyValue, AvgPeriodDays);
	FF_CumBuyShares = SumDays(FF_IntBuyShares, AvgPeriodDays);
	
	FF_BuyAvg = IIf(FF_CumBuyShares, FF_DayBuyValue / FF_CumBuyShares, Null);
	
	Plot( FF_BuyAvg, "BuyAvg(" + NumToStr(AvgPeriodDays,1.0) + ")", colorSeaGreen, styleDashed | styleThick | styleDots, Null, Null, Null, 2); 

	
	FF_IntSellValue = odbcGetArraySQL(" SELECT INT_SELL_VALUE, dtime " + 
								      " FROM v_adj_intraday_foreign " +
							          " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	FF_IntSellShares = FF_IntSellValue / Close;

	FF_DaySellValue = SumDays(FF_IntSellValue, AvgPeriodDays);
	FF_CumSellShares = SumDays(FF_IntSellShares, AvgPeriodDays);
	FF_SellAvg = IIf(FF_CumSellShares, FF_DaySellValue / FF_CumSellShares, Null);
	Plot( FF_SellAvg, 
		  "SellAvg(" + NumToStr(AvgPeriodDays,1.0) + ")", 
		  colorLightOrange, 
		  styleDashed | styleThick, 
		  Null, Null, Null, 2); 
	
_SECTION_NAME();
*/

//
// Foreign accummulation (balance)
//
_SECTION_BEGIN("Foreign Balance (B)");
	ffBalance = stGetFFBalanceFromInterval(ffIntervalValue);
	
	ffBalanceMin = LowestVisibleValue(ffBalance);
	ffBalanceMax = HighestVisibleValue(ffBalance);
	
	Plot( ffBalance, 
		  "Balance(B)", 
		  colorRed, 
		  styleLine | styleOwnScale | styleThick, 
		  ffBalanceMin, 
		  ffBalanceMax, 
		  Null, 
		  2); 
_SECTION_END();

//
// Foreign average price
//
_SECTION_BEGIN("Foreign Average");
	ffAvg = stGetFFAverageFromIntervalNet(ffIntervalValue);
	Plot( ffAvg, "Foreign Avg", colorGreen, styleDashed | styleThick | styleNoLabel); 
_SECTION_END();

//
// Foreign MA
//
_SECTION_BEGIN("Foreign MA");
	ffMaEnabled = ParamToggle("Enable Foreign MA", "No|Yes", 1);
	ffMAPeriod1 = Param("Foreign MA Period 1", 5, 1, 100);
	ffMAPeriod2 = Param("Foreign MA Period 2", 20, 1, 100);
	
	ffMA1 = MA(ffBalance, ffMAPeriod1);
	ffMA2 = MA(ffBalance, ffMAPeriod2);
	
	if (ffMaEnabled) {
		Plot( ffMA1, "fMA" + ffMAPeriod1, colorOrange, styleLine | styleOwnScale | styleNoLabel, ffBalanceMin, ffBalanceMax, Null, 2);
		Plot( ffMA2, "fMA" + ffMAPeriod2, colorOrange, styleDashed | styleOwnScale | styleNoLabel, ffBalanceMin, ffBalanceMax, Null, 2);
	}
_SECTION_END();

if (ffMaEnabled) {
	Buy = Cross( ffMA1, ffMA2) && (ffBalance >= ffMA1) && (C < ffAvg) && (ffIntervalValue > 1);
	//Plot(Buy, "Buy", colorBlack, styleNoLine | styleNoLabel | styleOwnScale);
	PlotShapes( Buy * shapeUpArrow, colorGreen, 0, Min(L, O)); 
}
