/* 
 * Copyright (C)2017 PT. Stosia Teknologi Investasi
 *
 * This file is licensed under  GNU GPL (General Public License) version 3.
 *
 * File ini dilisensikan kepada Anda, bukan  Anda miliki.  Pemilik file ini
 * adalah pemegang  hak cipta ("copyright holder") di atas. Gunakanlah file
 * ini sesuai dengan lisensi yang telah ditetapkan untuk file ini.
 *
 * File ini  berlisensi  GNU GPL (General Public License)  versi 3. Artinya
 * kalau Anda  memodifikasi file ini, atau membuat karya lain yang berbasis
 * file ini  ("derivative work"),  maka Anda  wajib  mendistribusikan  kode
 * modifikasi atau karya tersebut kepada publik. Hal ini untuk menjaga agar
 * pengetahuan yang ada di sini menjadi lebih berkembang  di masa mendatang 
 * untuk kemajuan kita bersama.
 *
 * Silakan memakai kode ini untuk kepentingan apapun termasuk untuk mencari
 * profit di pasar saham baik secara perorangan atau organisasi, asal tidak
 * jahat. Sebagai tambahan permintaan, kami menghimbau untuk TIDAK  menjual
 * kode/file  ini secara  apa adanya  atau dalam bundel  produk yang dijual
 * secara komersial, karena hal itu tidak etis.
 *
 * Untuk penjelasan lebih lanjut silakan bertanya kepada kami.  Untuk detil
 * lisensi GPLv3  silakan lihat file GPLv3-LICENSE.md  yang didistribusikan
 * bersama file ini.
 *
 * Author: Benny Prijono <benny@stosia.com>
 * Contributors:
 *  - 
 */
#include_once <..\Quant.id\AFL\Tools.afl>

//
// Foreign flow interval value is the foreign NET buy/sell for each
// interval, where interval can be intraday or daily. Interval greater than
// one day is not supported yet.
//
// Return:
//   foreign flow NET value array for each interval (in milyar Rp)
//
function stGetFFIntervalValue()
{
	stConnectDatabase();

	if (Interval() < inDaily) {
		ffIntVal = odbcGetArraySQL( " SELECT INT_NET_VALUE, dtime " + 
									 " FROM v_adj_intraday_foreign " +
									 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	} else {
		if (Interval() > inDaily) {
			stAlert("Error: Interval greater than daily is not supported yet");
		}
		
		ffIntVal = odbcGetArraySQL( " SELECT DAY_NET_VALUE, dtime " + 
									" FROM v_adj_intraday_foreign " +
									" WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	}
	
	return ffIntVal;
}

//
// Get foreign BUY value (in milyar Rp) for each interval
//
function stGetFFIntervalBuyValue()
{
	stConnectDatabase();

	if (Interval() < inDaily) {
		ffIntBuyValue = odbcGetArraySQL( " SELECT int_buy_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	} else {
		if (Interval() > inDaily) {
			stAlert("Error: Interval greater than daily is not supported yet");
		}
		
		ffIntBuyValue = odbcGetArraySQL( " SELECT day_buy_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	}

	return ffIntBuyValue;
}

//
// Get foreign SELL value (in milyar Rp) for each interval
//
function stGetFFIntervalSellValue()
{
	stConnectDatabase();

	if (Interval() < inDaily) {
		ffIntBuyValue = odbcGetArraySQL( " SELECT int_sell_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	} else {
		if (Interval() > inDaily) {
			stAlert("Error: Interval greater than daily is not supported yet");
		}
		
		ffIntBuyValue = odbcGetArraySQL( " SELECT day_sell_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	}

	return ffIntBuyValue;
}

//
// Get foreign balance from the interval value. Foreign balance is the total cummulative value 
// of foreign flow.
//
function stGetFFBalanceFromInterval(ffIntervalValue)
{
	if (Interval() < inDaily) {
		ffBalance = stSumDays(ffIntervalValue, 1);
	} else {
		ffBalance = Cum(ffIntervalValue);
	}
	
	return ffBalance;
}

//
// Get foreign balance. Foreign balance is the total cummulative value of foreign flow.
//
function stGetFFBalance()
{
	ffIntervalValue = stGetFFIntervalValue();
	return stGetFFBalanceFromInterval(ffIntervalValue);
}

//
// Get average price value of foreign ownership
//
function stGetFFAverageFromIntervalNet(ffIntVal)
{
	ffIntVal = ffIntVal * MILYAR;
	typ = stTypicalPrice();

	dn = DateNum();
	prev_dn = -1;
	isIntraday = Interval() < inDaily;
	
	if (Interval() > inDaily) {
		stAlert("Error: Interval greater than daily is not supported yet");
	}

	i = 0;
	if (ffIntVal[i] >= 0) {
		ffBalance[i] = ffIntVal[i];
		ffShares[i] = ffIntVal[i] / typ[i];
		ffAvg[i] = typ[i];
	} else {
		ffBalance[i] = 0;
		ffShares[i] = 0;
		ffAvg[i] = Null;
	}

	for (i=1; i<BarCount; ++i) {
		if (isIntraday && dn[i] != prev_dn) {
			// Reset on new day
			if (ffIntVal[i] >= 0) {
				ffBalance[i] = ffIntVal[i];
				ffShares[i] = ffIntVal[i] / typ[i];
				ffAvg[i] = typ[i];
			} else {
				ffBalance[i] = 0;
				ffShares[i] = 0;
				ffAvg[i] = Null;
			}
			
			prev_dn = dn[i];
		} else {
			ffBalance[i] = ffBalance[i-1] + ffIntVal[i];
			ffShares[i] = ffShares[i-1] + (ffIntVal[i] / typ[i]);
			
			if (ffBalance[i] < 0 || ffShares[i] < 0) {
				// All sold
				ffBalance[i] = 0;
				ffShares[i] = 0;
				ffAvg[i] = Null;
			} else if (i > 0 && ffIntVal[i] < 0) {
				// Sell operation doesn't change avg
				ffAvg[i] = ffAvg[i-1];
				// Adjust balance
				ffBalance[i] = ffShares[i] * ffAvg[i];
			} else {
				
				ffAvg[i] = ffBalance[i] / ffShares[i];
			}
		}
	}
		
	//Plot(ffIntVal, "ffIntVal", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	//Plot(typ, "typ", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	//Plot(ffBalance, "ffBalance", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	//Plot(ffShares, "ffShares", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	
	return ffAvg;
}

//
// Get foreign average stock price
//
function stGetFFAverage()
{
	ffIntVal = stGetFFIntervalValue();
	return stGetFFAverageFromIntervalNet(ffIntVal);
}
