/* 
 * Copyright (C) 2017 Benny Prijono <benny@stosia.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA 
 */
#include_once <..\..\quant.id\AFL\Tools.afl>

//
// Foreign flow interval value is the foreign NET buy/sell for each
// interval, where interval can be intraday or daily. Interval greater than
// one day is not supported yet.
//
// Return:
//   foreign flow NET value array for each interval (in milyar Rp)
//
function ffGetIntervalValue()
{
	odbcConnectDatabase();

	if (Interval() < inDaily) {
		ffIntVal = odbcGetArraySQL( " SELECT INT_NET_VALUE, dtime " + 
									 " FROM v_adj_intraday_foreign " +
									 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	} else {
		if (Interval() > inDaily) {
			printf("** WARNING **\nInterval greater than daily is incorrect!\n");
		}
		
		ffIntVal = odbcGetArraySQL( " SELECT DAY_NET_VALUE, dtime " + 
									" FROM v_adj_intraday_foreign " +
									" WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	}
	
	return ffIntVal;
}

//
// Get foreign BUY value (in milyar Rp) for each interval
//
function ffGetIntervalBuyValue()
{
	odbcConnectDatabase();

	if (Interval() < inDaily) {
		ffIntBuyValue = odbcGetArraySQL( " SELECT int_buy_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	} else {
		if (Interval() > inDaily) {
			printf("** WARNING **\nInterval greater than daily is incorrect!\n");
		}
		
		ffIntBuyValue = odbcGetArraySQL( " SELECT day_buy_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	}

	return ffIntBuyValue;
}

//
// Get foreign SELL value (in milyar Rp) for each interval
//
function ffGetIntervalSellValue()
{
	odbcConnectDatabase();

	if (Interval() < inDaily) {
		ffIntBuyValue = odbcGetArraySQL( " SELECT int_sell_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	} else {
		if (Interval() > inDaily) {
			printf("** WARNING **\nInterval greater than daily is incorrect!\n");
		}
		
		ffIntBuyValue = odbcGetArraySQL( " SELECT day_sell_value, dtime " + 
										 " FROM v_adj_intraday_foreign " +
										 " WHERE ticker='" + Name() + "' ORDER BY dtime ASC") / MILYAR;
	}

	return ffIntBuyValue;
}

//
// Get foreign balance from the interval value. Foreign balance is the total cummulative value 
// of foreign flow.
//
function ffGetBalanceFromInterval(ffIntervalValue)
{
	if (Interval() < inDaily) {
		ffBalance = SumDays(ffIntervalValue, 1);
	} else {
		ffBalance = Cum(ffIntervalValue);
	}
	
	return ffBalance;
}

//
// Get foreign balance. Foreign balance is the total cummulative value of foreign flow.
//
function ffGetBalance()
{
	ffIntervalValue = ffGetIntervalValue();
	return ffGetBalanceFromInterval(ffIntervalValue);
}

//
// Get average price value of foreign ownership
//
function ffGetAverageFromInterval(ffIntVal)
{
	ffIntVal = ffIntVal * MILYAR;
	typ = typicalPrice();

	dn = DateNum();
	prev_dn = -1;
	isIntraday = Interval() < inDaily;
	
	if (Interval() > inDaily) {
		printf("** WARNING **\nInterval greater than daily is not supported!\n");
	}

	i = 0;
	if (ffIntVal[i] >= 0) {
		ffBalance[i] = ffIntVal[i];
		ffShares[i] = ffIntVal[i] / typ[i];
		ffAvg[i] = typ[i];
	} else {
		ffBalance[i] = 0;
		ffShares[i] = 0;
		ffAvg[i] = Null;
	}

	for (i=1; i<BarCount; ++i) {
		if (isIntraday && dn[i] != prev_dn) {
			// Reset on new day
			if (ffIntVal[i] >= 0) {
				ffBalance[i] = ffIntVal[i];
				ffShares[i] = ffIntVal[i] / typ[i];
				ffAvg[i] = typ[i];
			} else {
				ffBalance[i] = 0;
				ffShares[i] = 0;
				ffAvg[i] = Null;
			}
			
			prev_dn = dn[i];
		} else {
			ffBalance[i] = ffBalance[i-1] + ffIntVal[i];
			ffShares[i] = ffShares[i-1] + (ffIntVal[i] / typ[i]);
			
			if (ffBalance[i] < 0 || ffShares[i] < 0) {
				// All sold
				ffBalance[i] = 0;
				ffShares[i] = 0;
				ffAvg[i] = Null;
			} else if (i > 0 && ffIntVal[i] < 0) {
				// Sell operation doesn't change avg
				ffAvg[i] = ffAvg[i-1];
				// Adjust balance
				ffBalance[i] = ffShares[i] * ffAvg[i];
			} else {
				
				ffAvg[i] = ffBalance[i] / ffShares[i];
			}
		}
	}
		
	//Plot(ffIntVal, "ffIntVal", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	//Plot(typ, "typ", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	//Plot(ffBalance, "ffBalance", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	//Plot(ffShares, "ffShares", colorBlack, styleNoLine | styleOwnScale | styleNoLabel);
	
	return ffAvg;
}

function ffGetAverage()
{
	ffIntVal = ffGetIntervalValue();
	return ffGetAverageFromInterval(ffIntVal);
}
