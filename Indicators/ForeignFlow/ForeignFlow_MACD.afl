/* 
 * Copyright (C) 2017 Benny Prijono <benny@stosia.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA 
 */
#include_once <..\..\quant.id\AFL\Tools.afl>
#include_once <..\..\quant.id\AFL\Common.afl>
#include_once <..\..\quant.id\AFL\ForeignFlow.afl>


_SECTION_BEGIN("Foreign MACD");

	SetBackgroundText("Foreign MACD");

	maPeriod1 = Param("EMA 1", 12, 1, 200, 1);
	maPeriod2 = Param("EMA 2", 26, 1, 200, 1);
	maPeriod3 = Param("EMA 3", 9, 1, 200, 1);
	
	ffBalance = ffGetBalance();
	
	// For intraday chart, only show MACD for today
	if (Interval() < inDaily) {
		Dn = DateNum();
		Last_DN = Dn[BarCount-1];
										 
		ffBalance = IIf(Dn == Last_DN, ffBalance, 0);
	}
	
	EMA_L = EMA(ffBalance, maPeriod2);
	EMA_S = EMA(ffBalance, maPeriod1);
	
	F_MACD = EMA_S - EMA_L;
	Sig = EMA(F_MACD, maPeriod3);
	
	Plot(F_MACD, "MACD", ColorBlend( colorBlue, GetChartBkColor(), 0.5  ), styleLine | styleThick);
	Plot(Sig, "Signal", ColorBlend( colorBlue, GetChartBkColor(), 0.5  ), styleDashed);
	
	Hist = F_MACD - Sig;
	AHist = abs(Hist);
	Plot(AHist, "Hist", IIf(Hist >= 0, colorLightBlue, colorLightOrange), styleHistogram | styleOwnScale, 0, HighestVisibleValue(AHist) * 2, 0, 0, 4);
	
	Buy = Cross(F_MACD, Sig);
	Sell = Cross(Sig, F_MACD);
	PlotShapes( Buy * shapeUpArrow, colorGreen, 0, Sig); 
	PlotShapes( Sell * shapeDownArrow, colorRed, 0, F_MACD);
	
_SECTION_END();

